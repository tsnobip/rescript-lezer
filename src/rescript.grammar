@precedence {
  member,
  call,
  prefix,
  times @left,
  plus @left,
  concat @left,
  pipe @left,
  rel @left,
  and @left,
  or @left,
  typearg @left,
  arrow @right,
  statement @cut
}

@top Script { statement* }

@top SingleExpression { expression }

statement[@isGroup=Statement] {
  LetBinding |
  ModuleDeclaration |
  TypeDeclaration |
  expression
}

ModuleDeclaration {
  kw<"module"> ConstructorName "=" ModuleBody
}

ModuleBody {
  "{" statement* "}"
}

LetBinding {
  kw<"let"> (kw<"rec">)? VariableDefinition parameterList? (":" type)? "=" expression
}

parameterList {
  "(" commaSep<parameter> ")"
}

parameter {
  LabeledParameter |
  OptionalParameter |
  VariableName
}

LabeledParameter {
  "~" VariableName
}

OptionalParameter {
  "~" VariableName "=" "?"
}

SwitchExpression[@dynamicPrecedence=1] {
  kw<"switch"> expression "{" SwitchCase* "}"
}

SwitchCase[@dynamicPrecedence=-1] {
  "|" Pattern (PatternGuard)? "=>" expression
}

// Pattern guards may start with `if` or `when`. Any expression is allowed
// inside the guard, so we accept a bare `expression` here. `if` is preferred.
PatternGuard {
  kw<"if"> expression |
  kw<"when"> expression
}

Pattern {
  Wildcard { "_" } |
  VariableName |
  Number |
  String |
  boolean |
  ConstructorPattern |
  ArrayPattern |
  RecordPattern |
  ParenthesizedPattern
}

ConstructorPattern {
  ConstructorName ("(" commaSep<Pattern> ")")?
}

ArrayPattern {
  "[" commaSep<Pattern> "]"
}

RecordPattern {
  "{" commaSep<PatternField> "}"
}

PatternField {
  PropertyName (":" Pattern)?
}

ParenthesizedPattern {
  "(" Pattern ")"
}

expression[@isGroup=Expression] {
  Number |
  String |
  TemplateString |
  Char |
  boolean |
  VariableName |
  ConstructorName |
  ArrayExpression { "[" commaSep<expression> "]" } |
  ListExpression { kw<"list"> "{" commaSep<expression> "}" } |
  DictExpression { kw<"dict"> "{" commaSep<DictEntry> "}" } |
  RecordExpression { "{" commaSep<Property> "}" } |
  Decorator { "@" VariableName } |
  ExtensionExpression { "%" VariableName "(" commaSep<expression> ")" } |
  SwitchExpression |
  ArrowFunction[@dynamicPrecedence=1] {
    (
      VariableName |
      ParamList
    ) !arrow "=>" expression
  } |
  CallExpression { expression !call "(" commaSep<expression> ")" } |
  MemberExpression { expression !member "." (PropertyName | ConstructorName) } |
  BinaryExpression {
    expression !times (ArithOp<"*"> | ArithOp<"/"> | ArithOp<"*."> | ArithOp<"/."> | kw<"mod">) expression |
    expression !plus (ArithOp<"+"> | ArithOp<"-"> | ArithOp<"+."> | ArithOp<"-.">) expression |
    expression !concat StringOp<"++"> expression |
    expression !pipe PipeOp<"->" | "|>"> expression |
    expression !rel (LessThan | CompareOp<"<=" | ">" | ">=" | "==" | "!=">) expression |
    expression !and LogicOp<"&&"> expression |
    expression !or LogicOp<"||"> expression
  } |
  UnaryExpression { !prefix (ArithOp<"-"> | LogicOp<"!">) expression } |
  ParenthesizedExpression { "(" expression ~parenthesized ")" } |
  JSXElement
}

DictEntry {
  String ":" expression
}

Property {
  PropertyDefinition ":" expression
}

ParamList {
   "(" commaSep<parameter> ~parenthesized ")"
}

ParenthesizedType { "(" type ")" }

Quote { "'" }
Hash { "#" }

TypeParameter { Quote VariableName }

FunctionType { (TypeName | TypeParameter | ParenthesizedType) "->" type }

type[@isGroup=Type] {
  TypeName |
  TypeParameter |
  VariantType |
  TypeApplication |
  ParenthesizedType |
  FunctionType
}


TypeAngleLeftNode { CompareOp<"<"> }
TypeAngleRightNode { CompareOp<">"> }

TypeApplication { TypeName TypeAngleLeftNode commaSep<type> TypeAngleRightNode }

TypeParams { TypeAngleLeftNode commaSep<TypeParam> TypeAngleRightNode }

TypeParam { "_" | VariableDefinition | TypeParameter }

VariantTag { Hash ConstructorName }

// Variance marker for polymorphic variants: lower-bound '>' or upper-bound '<'
VarianceLeftNode { CompareOp<"<"> }
VarianceRightNode { CompareOp<">"> }
VarianceMarker { VarianceRightNode | VarianceLeftNode }

// VariantType: one-or-more variant tags separated by pipe `|` (e.g. [#A | #B])
VariantType {
  "[" VarianceMarker? VariantTag ("|" VariantTag)* "]" (kw<"as"> TypeParameter)?
}

// Type declaration supports optional `rec` and optional type parameters
TypeDeclaration {
  kw<"type"> (kw<"rec">)? TypeName (TypeParams)? "=" (SumType | type)
}

SumType {
  "|" Constructor ("|" Constructor)*
}

Constructor {
  ConstructorName !typearg ("(" type ")" ":" type | ":" type)?
}

commaSep<content> {
  "" | content ("," content?)*
}

// Keywords
kw<term> { @specialize[@name={term}]<identifier, term> }

boolean { @specialize[@name=BooleanLiteral]<identifier, "true" | "false"> }

VariableName { identifier }
VariableDefinition { identifier }
TypeName { identifier }
ConstructorName { uppercaseIdentifier }
PropertyName { identifier }
PropertyDefinition { identifier }

@skip { spaces | newline | LineComment | BlockComment }

@context trackNewline from "./tokens.js"

@skip {} {
  String[isolate] {
    '"' (stringContentDouble | Escape)* ('"' | "\n")
  }
  
  TemplateString[isolate] {
    "`" (TemplateContent | Interpolation)* "`"
  }
  
  BlockComment[isolate] { "/*" (blockCommentContent | blockCommentNewline)* blockCommentEnd }
}

TemplateContent {
  templateText
}

Interpolation {
  "${" expression "}"
}

@local tokens {
  blockCommentEnd { "*/" }
  blockCommentNewline { "\n" }
  @else blockCommentContent
}

@tokens {
  spaces[@export] { $[\u0009 \u000b\u00a0\u1680\u2000-\u200a\u202f\u205f\u3000\ufeff]+ }
  newline[@export] { $[\r\n\u2028\u2029] }

  LineComment[isolate] { "//" ![\n]* }

  identifierChar { @asciiLetter | $[_$] }
  
  identifier { $[a-z_] (identifierChar | @digit)* }
  
  uppercaseIdentifier { $[A-Z] (identifierChar | @digit)* }

  @precedence { spaces, newline, identifier, "_" }

  Number {
    (@digit ("_" | @digit)* ("." ("_" | @digit)*)? | "." @digit ("_" | @digit)*)
      (("e" | "E") ("+" | "-")? ("_" | @digit)+)? |
    @digit ("_" | @digit)* "n" |
    "0x" $[0-9a-fA-F_]+ |
    "0b" $[01_]+ |
    "0o" $[0-7_]+
  }

  @precedence { Number, "." }

  Char { "'" (![\\\n'] | "\\" _) "'" }

  Escape {
    "\\" ("x" $[0-9a-fA-F] $[0-9a-fA-F] | "u" ("{" $[0-9a-fA-F]+ "}" | $[0-9a-fA-F] $[0-9a-fA-F] $[0-9a-fA-F] $[0-9a-fA-F]) | ![xu])
  }

  stringContentDouble { ![\\\n"]+ }

  ArithOp<expr> { expr }
  LogicOp<expr> { expr }
  CompareOp<expr> { expr }
  StringOp<expr> { expr }
  PipeOp<expr> { expr }
  
  templateText { ![\`$]+ | "$" ![\{] }

  @precedence { LineComment, ArithOp }

  "=>"
  "("  ")" "[" "]" "{" "}"
  "." "," ":" ";"
  "=" "?" "|" "~" "'" "@" "%" "_" "`" "${"

  "+." "-." "*." "/."

  JSXEndTag { ">" }
  
  JSXSelfCloseEndTag { "/>" }
  JSXStartCloseTag { "</" }
  JSXText { ![<{]+ }
  JSXIdentifier { identifier }
}

@skip {} {
  JSXElement {
    JSXSelfClosingTag |
    JSXOpenTag jsxChildren* JSXCloseTag
  }
}

jsxChildren {
  JSXText |
  JSXElement |
  JSXExpressionContainer
}

JSXExpressionContainer {
  "{" expression "}"
}

JSXSelfClosingTag { JSXStartTag jsxElementName jsxAttribute* JSXSelfCloseEndTag }
JSXOpenTag { JSXStartTag jsxElementName jsxAttribute* JSXEndTag }
JSXCloseTag { JSXStartCloseTag jsxElementName? JSXEndTag }

jsxElementName {
  JSXIdentifier |
  JSXMemberExpression
}

JSXMemberExpression { (JSXMemberExpression | JSXIdentifier) "." JSXIdentifier }

jsxAttribute {
  JSXAttribute { JSXIdentifier ("=" jsxAttributeValue)? }
}

jsxAttributeValue {
  String |
  "{" expression "}"
}

@external propSource rescriptHighlight from "./highlight"

@external tokens jsx from "./tokens" { JSXStartTag, LessThan[@name=CompareOp] }

@detectDelim
